* program03.sas ;
* Descriptive procedures, plots;

* On line 8, you must change the file location address to the exact location, including the filename and file extension, where you saved the file, tomhs.data, on your machine. Use the forward slash, / ,  as on line 7.;
* At the INPUT statement, ptid starts at the first character there are 10 spaces reserved for the ptid. Then a space. The variable clinic starts at the 12th character, from left to right. There is 1 space reserved for clinic. ;
DATA weight;
 *INFILE 'C:\SAS_Files\tomhs.data'; 
 INFILE '~/SAS_Files/tomhs.data';
 INPUT @1  ptid $10. 
       @12 clinic $1.
       @25 group 1.
       @30 sex $1. 
       @58 height 4. 
       @85 weight 5.
       @115 sbpbl 3. 
       @123 sbp12 3.;
 bmi = (weight*703.0768)/(height*height);
RUN;

* OBS = the number of observations to print;
PROC PRINT DATA = weight (OBS=5) ;
VAR  ptid clinic sex height weight bmi;
TITLE 'Proc Print: Five observations from the TOMHS Study';
RUN;

PROC MEANS DATA = weight ;
 VAR  height weight bmi;
 TITLE 'Proc Means Example 1';
RUN;

* MAXDEC option limits number of decimal places displayed. In this example the number of decimals is 2;
PROC MEANS DATA = weight MEAN MEDIAN STD MAXDEC=2 ;
 VAR  height weight bmi;
 TITLE 'Proc Means Example 2 (specifying options)';
RUN;

* Get statistics separately for each clinical center. The FW option limits the size of the columns of output displayed;
* The CLASS statement is used when there is a character or categorical variable you want to output statistiscal metrics for each categorical level;
PROC MEANS DATA = weight N MEAN STD MAXDEC=2 FW=8;
 VAR  height weight bmi;
 CLASS clinic; 
 TITLE 'Proc Means Example 3 (Using a CLASS statement)';
RUN;

* WAYS statement specifies the number of ways to make unique combinations of classification variable CLASS. ;
* USing WAYS creates a one-way types for the classification variable CLASS. It also creates an overall output.;
* We get the “one-way” analysis of each CLASS variable; that is, the description is broken out by the CLASS variable, 'clinic';
PROC MEANS DATA = weight N MEAN STD MAXDEC=2 FW=8;
 VAR  height weight bmi;
 CLASS clinic;
 WAYS 0 1;
 TITLE 'Proc Means Example 3B (Using a CLASS statement)';
RUN;

* Could also sort the data by clinic and then use BY statement;
PROC SORT data=weight; BY clinic;
PROC MEANS DATA = weight N MEAN STD MAXDEC=2 FW=8;
 VAR  height weight bmi;
 TITLE 'Proc Means Example 4 (Using a BY statement)';
 BY clinic;
RUN;

* T-test Comparing Active and Placebo Groups;
PROC TTEST DATA = weight ;
 CLASS active;
 VAR  sbpchg;
RUN;

* The nextrobs option is used to request a different number of extreme observations that PROC UNIVARIATE lists in the table of extreme observations.;
* Use 'nextrobs'as a simple way to see the largest and smallest observations in the variable.;
PROC UNIVARIATE DATA = weight PLOT nextrobs=10  ;
 ID ptid;
 VAR bmi;
 TITLE 'Proc Univariate Example 1';
RUN;

*---------------------------------------------------------------------------*
  High resolution graphs can also be produced.  
  The HISTOGRAM statement produces a high resolution histogram. The 
  PROBPLOT statement creates a high resolution normal probability plot.
  The INSET statement inserts summary statistics as part of the histogram.
  The MIDPOINTS can be changed minimum to maximam by a certain value (i.e. 2, 5, or 10)
  The NORMAL option is to output a histogram with a normal distribution (i.e. the bell curve)
*---------------------------------------------------------------------------*;
ods graphics on;
PROC UNIVARIATE DATA = weight ;
 VAR bmi;
 HISTOGRAM bmi / NORMAL MIDPOINTS=20 to 40 by 2;
 INSET N    = 'N' (5.0)
       MEAN = 'Mean' (5.1)
       STD  = 'Sdev' (5.1)
       MIN  = 'Min'  (5.1)
       MAX  = 'Max'  (5.1)/ POS=NW HEADER='Summary Statistics';
 LABEL bmi = 'Body Mass Index (kg/m2)';
 TITLE 'Histogram and Normal Plot of BMI';
 PROBPLOT bmi/NORMAL (MU=est SIGMA=est);
RUN;

* To output statistical graphs you can use the SGPLOT statement ;
* SGPLOT procedure creates one or more plots and overlays them on a single set of axes;
* This example has both a histogram and a density plot in one plot space;
PROC SGPLOT;
 HISTOGRAM bmi;
 DENSITY bmi/type=normal;
 TITLE 'Histogram of BMI';
RUN;

PROC SGPLOT;
 hBOX BMI; 
 TITLE 'Boxplot of BMI';
RUN;

PROC SGPLOT;
 VBOX bmi;
 TITLE 'Boxplot of BMI';
RUN;

* A scatterplot, SCATTER, is used to graphically see the distribution of two numeric variables, see below, height and weight;
PROC SGPLOT;
 SCATTER X=height Y=weight;
 TITLE 'Weight vs Height';
run;

* The HBOX statement outputs a horizontal boxplot;
PROC SGPLOT;
 TITLE "Boxplot of BMI for Men and Women";
  HBOX bmi/CATEGORY=sex;
RUN;

*---------------------------------------------------------------------------*
  With the Output Delivery System (ODS) you can selectively include portions
  of the output to go to the output window, thus reducing the amount of output
  generated. Each piece of output has a name that can be selected.
*---------------------------------------------------------------------------*;

*ODS TRACE ON/LISTING; * Lists the names of the pieces of output;
ODS SELECT BasicMeasures Quantiles ; 
PROC UNIVARIATE DATA = weight ;
 ID ptid;
 VAR bmi;
 TITLE 'Proc Univariate Selecting Only the Basic Measures and Quantiles';
RUN;
